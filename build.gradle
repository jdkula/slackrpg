buildscript {
    ext.kotlin_version = '1.2.60'
    ext.ktor_version = '0.9.2'

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://kotlin.bintray.com/kotlinx" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.4"
    }
}

allprojects {
    apply plugin: 'kotlin'
    apply plugin: 'application'
    group 'pw.jonak.slackrpg'
    version '1.0-SNAPSHOT'
    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://kotlin.bintray.com/kotlinx" }
        maven { url "https://dl.bintray.com/kotlin/ktor" }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        implementation 'com.beust:klaxon:3.0.1'
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    kotlin { experimental { coroutines "enable" } }

}

configure(subprojects.findAll { it.name != 'slackutil' }) {
    apply plugin: "com.github.johnrengelman.shadow"

    repositories {
        maven { url "https://dl.bintray.com/kotlin/exposed" }
    }

    dependencies {
        compile "io.ktor:ktor-server-netty:$ktor_version"
        compile "io.ktor:ktor-server-host-common:$ktor_version"
        compile "ch.qos.logback:logback-classic:1.2.1"
        compile 'org.jetbrains.exposed:exposed:0.10.5'
        compile 'org.xerial:sqlite-jdbc:3.23.1'
        compile project(':slackutil')
    }

    mainClassName = 'io.ktor.server.netty.DevelopmentEngine'

}